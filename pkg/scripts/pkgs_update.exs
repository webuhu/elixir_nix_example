defmodule PkgsUpdate do
  @moduledoc """
  Updates the local pkg/_pkgs.nix file with the latest Nix package from Github.
  """

  @nix_pkg_download_url "https://github.com/NixOS/nixpkgs-channels/archive"
  @github_channel "nixos-unstable-small"

  @doc """
  Run the pkg/_pkgs.nix update generation.
  """
  def run() do
    commit_sha =
      github_api_request(github_api_url())
      |> get_latest_commit_sha()

    pkg_sha = get_nix_pkg_sha(commit_sha)
    write_pkgs_file(commit_sha, pkg_sha)
  end

  defp github_api_url() do
    "https://api.github.com/repos/NixOS/nixpkgs-channels/commits?sha=#{@github_channel}"
  end

  @spec github_api_request(binary()) :: charlist()
  defp github_api_request(github_api_url) do
    request = {to_charlist(github_api_url), [{'User-Agent', 'httpc'}]}

    :inets.start()
    :ssl.start()

    {:ok, {_, _, body}} =
      :httpc.request(
        :get,
        request,
        [],
        body_format: :binary
      )

    body
  end

  @spec get_latest_commit_sha(binary()) :: binary()
  defp get_latest_commit_sha(contents) do
    commit_sha_list =
      for {start, _end} <- :binary.matches(contents, "\"sha\":") do
        # Build the lookup scope from after the key until the end of the binary
        scope_start = start + 6
        scope_end = byte_size(contents) - scope_start

        # Find the next quote
        {start, _} = :binary.match(contents, "\"", scope: {scope_start, scope_end})

        # Extract SHA which is always 40 bytes
        :binary.part(contents, start + 1, 40)
      end

    List.first(commit_sha_list)
  end

  @spec get_nix_pkg_sha(binary()) :: binary()
  defp get_nix_pkg_sha(commit_sha) do
    nix_pkg_url = @nix_pkg_download_url <> "/#{commit_sha}.tar.gz"
    {pkg_sha, _} = System.cmd("nix-prefetch-url", ["--unpack", nix_pkg_url])
    String.trim_trailing(pkg_sha)
  end

  @spec write_pkgs_file(binary(), binary()) :: :ok
  defp write_pkgs_file(commit_sha, pkg_sha) do
    date = Date.utc_today()

    # nix derivation
    update = """
    # generated by pkgs_update.exs (#{date})
    import(fetchTarball {
      name = "#{@github_channel}_#{date}";
      url = "#{@nix_pkg_download_url}/#{commit_sha}.tar.gz";
      sha256 = "#{pkg_sha}";
    }) {}
    """

    # overwrite dervation
    File.write!("pkg/_pkgs.nix", update, [:write])
  end
end

PkgsUpdate.run()
